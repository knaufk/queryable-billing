import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.bmuschko.docker-remote-api'

dependencies {
    compile(project(':qb-common'),
            "org.apache.flink:flink-streaming-java_2.11:$flinkVersion",
            "org.apache.flink:flink-java:$flinkVersion",
            "org.apache.flink:flink-connector-filesystem_2.11:$flinkVersion",
            "org.apache.flink:flink-connector-kafka-0.10_2.11:$flinkVersion")

    testCompile "org.apache.flink:flink-test-utils_2.11:$flinkVersion"
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.tngtech.qb.QueryableBillingJob'
    }
}

def cert = new File(System.properties['user.home'], '.docker/machine/machines/default')
if (cert.exists()) {
    docker{
        certPath = cert
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn shadowJar
    destFile = project.file('build/Dockerfile')
    from 'qb-flink'
    addFile("libs/${shadowJar.archiveName}", ".")
    user("root")
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "${project.name}:${project.version}"
}